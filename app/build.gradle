apply plugin: 'com.android.application'
apply plugin: 'android-rocket-launcher'

// get signing properties
Properties keystoreProperties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    if (localProperties.getProperty('signingPropertiesFile') != null
            && new File(localProperties.getProperty('signingPropertiesFile')).exists()) {
        keystoreProperties.load(
                new FileInputStream(file(localProperties.getProperty('signingPropertiesFile'))))
    }
}
if (keystoreProperties.isEmpty()) {
    keystoreProperties.setProperty('storeFile', 'empty.keystore')
    keystoreProperties.setProperty('storePassword', 'storePassword')
    keystoreProperties.setProperty('keyAlias', 'keyAlias')
    keystoreProperties.setProperty('keyPassword', 'keyPassword')
}

android {
    compileSdkVersion 34
    namespace "de.eric_scheibler.tactileclock"

    androidResources {
        generateLocaleConfig true
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 34
        multiDexEnabled true
        versionCode 10
        versionName '2.2.0'
        // project website
        buildConfigField 'String', 'CONTACT_EMAIL_ADDRESS', '"tactile-clock@eric-scheibler.de"'
        buildConfigField 'String', 'PROJECT_WEBSITE', '"https://github.com/scheibler/TactileClock"'
        // set .apk name
        setProperty("archivesBaseName", "${archivesBaseName}-$versionName")
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
        }
    }

    lintOptions {
        disable "GoogleAppIndexingWarning", "Autofill", "IconLauncherShape", "ButtonStyle"
    }
}

dependencies {
    // android x
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core:1.12.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.fragment:fragment:1.6.2'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    // material design
    implementation 'com.google.android.material:material:1.11.0'
    // guava
    implementation 'com.google.guava:guava:31.0.1-android'
    // logging
    implementation 'com.jakewharton.timber:timber:4.7.1'
}

configurations.implementation {
    // must exclude that jdk below, otherwise classes get duplicated
    // required since update of androidx.core dependency to version 1.10.0
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

