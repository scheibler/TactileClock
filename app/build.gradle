apply plugin: 'com.android.application'

// get signing properties
Properties keystoreProperties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    if (localProperties.getProperty('signingPropertiesFile') != null
            && new File(localProperties.getProperty('signingPropertiesFile')).exists()) {
        keystoreProperties.load(
                new FileInputStream(file(localProperties.getProperty('signingPropertiesFile'))))
    }
}
if (keystoreProperties.isEmpty()) {
    keystoreProperties.setProperty('storeFile', 'empty.keystore')
    keystoreProperties.setProperty('storePassword', 'storePassword')
    keystoreProperties.setProperty('keyAlias', 'keyAlias')
    keystoreProperties.setProperty('keyPassword', 'keyPassword')
}

android {
    compileSdkVersion 36
    namespace "de.eric_scheibler.tactileclock"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
        resConfigs "en", "de"
        multiDexEnabled true
        versionCode 12
        versionName '2.4.0'
        // project website
        buildConfigField 'String', 'CONTACT_EMAIL_ADDRESS', '"tactile-clock@eric-scheibler.de"'
        buildConfigField 'String', 'PROJECT_WEBSITE', '"https://github.com/scheibler/TactileClock"'
        // set .apk name
        setProperty("archivesBaseName", "${archivesBaseName}-$versionName")
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
        }
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
    }

    lintOptions {
        //disable "GoogleAppIndexingWarning", "Autofill", "IconLauncherShape", "ButtonStyle"
        disable "InflateParams", "GoogleAppIndexingWarning"
        enable "NewerVersionAvailable"
        abortOnError true
    }
}

dependencies {
    // android x
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core:1.16.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.fragment:fragment:1.8.8'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'

    // material design
    implementation 'com.google.android.material:material:1.12.0'

    // guava
    implementation 'com.google.guava:guava:33.4.8-android'

    // logging
    implementation 'com.jakewharton.timber:timber:5.0.1'
}

// Open the APK on your Android Device
ext {
    mainActivityClass = 'de.eric_scheibler.tactileclock.ui.activity.MainActivity'
}
apply from: 'gradle-scripts/openApkTask.gradle'

